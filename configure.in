dnl
dnl   configuration script for hamsterdb
dnl
dnl   (C) 2006, Christoph Rupp, chris@crupp.de
dnl
dnl   see file LICENSE for licence information
dnl

AC_PREREQ(2.59)

dnl Get the version number of hamsterdb
define([ham_version], [esyscmd(build/scripts/get-version.sh src/version.h)])
AC_INIT([hamsterdb], 0.3.1, [chris@crupp.de])

AC_CONFIG_AUX_DIR(build/scripts)
AM_INIT_AUTOMAKE([1.9 foreign])
AC_CONFIG_HEADER(config.h)

AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_INSTALL
AC_HEADER_STDC
AC_C_BIGENDIAN
AC_C_CONST
AC_TYPE_SIZE_T
AC_CHECK_FUNCS(pread pwrite mmap munmap)
AC_CHECK_HEADERS(fcntl.h unistd.h)
AC_TYPE_OFF_T
AC_FUNC_MMAP

dnl
dnl big endian or little endian?
dnl 
AC_C_BIGENDIAN(
        big_endian=yes,
        big_endian=no,
        AC_MSG_ERROR(Don't know if this platform is bigendian or little
                     endian))
AM_CONDITIONAL(BIG_ENDIAN, test x$big_endian = xyes)

dnl
dnl support debugging
dnl 
AC_ARG_ENABLE(debug,
     [  --enable-debug          Turn on debugging],
     [case "${enableval}" in
       yes) debug=true ;;
       no)  debug=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
     esac],[debug=false])
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

dnl
dnl support profiling
dnl 
AC_ARG_ENABLE(profile,
     [  --enable-profile        Turn on profiling],
     [case "${enableval}" in
       yes) profile=true ;;
       no)  profile=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-profile) ;;
     esac],[profile=false])
AM_CONDITIONAL(PROFILE, test x$profile = xtrue)

dnl
dnl check for db.h - don't build ./tests if it's missing
dnl
AC_CHECK_HEADER(db.h,
        have_db_h=yes,
        have_db_h=no)
AC_MSG_NOTICE([Checking for berkeleyDb (db.h); if berkeleyDb is missing, the tests will not be built.])
AM_CONDITIONAL(HAVE_DB_H, test x$have_db_h = xyes)


AC_CONFIG_FILES(Makefile src/Makefile include/Makefile include/ham/Makefile samples/Makefile tests/Makefile)
AC_OUTPUT

