dnl
dnl configuration script for hamsterdb
dnl
dnl Copyright (C) 2005-2007 Christoph Rupp (chris@crupp.de).
dnl All rights reserved. See file LICENSE for licence and copyright
dnl information
dnl

AC_PREREQ(2.59)

dnl Get the version number of hamsterdb
define([ham_version], [esyscmd(build/scripts/get-version.sh src/version.h)])
AC_INIT([hamsterdb], 0.4.0, [chris@crupp.de])

AC_CONFIG_AUX_DIR(build/scripts)
AM_INIT_AUTOMAKE([1.9 foreign])
AM_CONFIG_HEADER(config.h)

AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_INSTALL
AC_HEADER_STDC
AC_C_BIGENDIAN
AC_C_CONST
AC_TYPE_SIZE_T
AC_CHECK_FUNCS(pread pwrite mmap munmap)
AC_CHECK_HEADERS(fcntl.h unistd.h)
AC_TYPE_OFF_T
AC_FUNC_MMAP

dnl
dnl use -Wall on platforms with gcc
dnl
if test "x$GCC" = "xyes"; then
    if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
        CFLAGS="${CFLAGS} -Wall"
    fi
fi

dnl 
dnl check for cygwin/Win32
dnl
case $host in 
*-*-cygwin*)
    CFLAGS="${CFLAGS} -DWIN32 -DCYGWIN"
    ;;
*-*-mingw32*)
    CFLAGS="${CFLAGS} -DWIN32"
    ;;
esac

dnl
dnl big endian or little endian?
dnl 
AC_C_BIGENDIAN(
        big_endian=yes,
        big_endian=no,
        AC_MSG_ERROR(Don't know if this platform is bigendian or little
                     endian))
AM_CONDITIONAL(BIG_ENDIAN, test x$big_endian = xyes)

dnl
dnl support debugging
dnl 
AC_ARG_ENABLE(debug,
     [  --enable-debug          Turn on debugging],
     [case "${enableval}" in
       yes) debug=true ;;
       no)  debug=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
     esac],[debug=false])
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

dnl
dnl support profiling
dnl 
AC_ARG_ENABLE(profile,
     [  --enable-profile        Turn on profiling],
     [case "${enableval}" in
       yes) profile=true ;;
       no)  profile=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-profile) ;;
     esac],[profile=false])
AM_CONDITIONAL(PROFILE, test x$profile = xtrue)

AC_CONFIG_FILES(Makefile src/Makefile include/Makefile include/ham/Makefile samples/Makefile)
AC_OUTPUT

